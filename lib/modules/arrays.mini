/*************************************************************************
  * A library with standard array functions. Never had to implement a    *
  * standard lib before so I'm sure these are all horibbly inefficient   *
  * and buggy                                                            *
  * @author: Zachary Marion                                              *
  * @date: November 30, 2016                                             *
  ***********************************************************************/

export fun concat(arr_a, arr_b) {
  ==/ Concatenate two arrays together /==
  for (el in arr_b) { arr_a << el }
  return arr_a
}

export fun map(arr, func) {
  ==/ Map an array through a given function /==
  let ret = []
  for (el in arr) { ret << func(el) }
  return ret
}

export fun each(arr, func) {
  ==/ Apply a function to each element in an array /==
  for (el in arr) {
    func(el) 
  }
}

export fun max(arr) {
  ==/ Compute the max element in an array /==
  if (len(arr) == 0) { return nada }
  let mut max = arr[0]
  for (el in arr) { if (el > max) { max = el }}
  return max
}

export fun min(arr) {
  ==/ Compute the min element in an array /==
  if (len(arr) == 0) { return nada }
  let mut min = arr[0]
  for (el in arr) { if (el < min) { min = el }}
  return min
}

export fun sum(arr) {
  ==/ Compute the sum of each element in the array /==
  let mut sum = 0
  for (el in arr) { sum = sum + el}
  return sum
}

export fun filter(arr, func) {
  ==/ Filter an array through a given function /==
  let ret = []
  for (el in arr) {
    if (func(el)) { ret << el }
  }
  return ret
}

export fun reduce(arr, func) {
  ==/ Reduce a function into a single expression /==
  let mut ret = nada 
  if (len(arr) == 0) { nada }
  elseif (len(arr) == 1) { ret = arr[0] }
  else { 
    ret = arr[0]
    for (el in slice(arr, 1, len(arr))) { 
      ret = func(ret, el)
    }
  }
  return ret
}

export fun empty(arr) {
  ==/ Whether an array is empty /==
  return len(arr) == 0
}

# Just another way to make a function
let main = () => {
  # Testing map
  let test = [1, 2, 3, 4]  
  let new = map(test, (a) => { a + 1 })
  println(new)

  # Testing range
  println(range(0, 10))

  # Testing filter
  let even = filter(test, (a) => { a % 2 == 0 })
  println(even)

  # Testing reduce
  let sum = reduce(test, (a, b) => { a + b })
  println(sum)
}

# Call the main function if file is not a module
if (__name == "main") {
  main()
}
